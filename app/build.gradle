apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.kpa.demo"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  dataBinding {
    enabled = true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
}

// 替换AndroidManifest 参数
// https://developer.android.com/studio/known-issues#variant_api
android.applicationVariants.all { variant ->
  variant.outputs.all { output ->
    output.processManifest.doLast {
      // Stores the path to the maifest.

      String manifestPath = "${manifestOutputDirectory.asFile.get()}/AndroidManifest.xml"

      // 替换one drive 配置
      def updatedContent = file(manifestPath).getText('UTF-8')
          .replaceAll("msal_package_name", "com.kpa.demo") // 包名
          .replaceAll("msal_signature_hash", "5qMkIKbIM5Lx7VbajX8mT3rKXpE=") // 当前签名hash
      file(manifestPath).write(updatedContent, 'UTF-8')
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  // 测试模块
  testImplementation rootProject.ext.junit
  androidTestImplementation rootProject.ext.runner
  androidTestImplementation rootProject.ext.androidXJunit

  implementation 'com.arialyy.keepassA:KeepassIcon:0.0.3'
  implementation "com.arialyy.keepassA:KeepassLib:0.0.4"
  implementation 'com.arialyy.keepassA:KPAFrame:0.0.5'
  implementation 'com.arialyy.keepassA:UIWidget:0.0.3'

  // kotlin
  implementation "androidx.core:core-ktx:${rootProject.ext.ktxVersion}"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.coroutinesVersion}"

  implementation "androidx.appcompat:appcompat:${rootProject.ext.appcompatVersion}"
  implementation "com.google.android.material:material:${rootProject.ext.materialVersion}"
  implementation "androidx.recyclerview:recyclerview:${rootProject.ext.recyclerviewVersion}"
  implementation "androidx.multidex:multidex:${rootProject.ext.multidexVersion}"
  implementation "androidx.cardview:cardview:${rootProject.ext.cardviewVersion}"
  implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintlayoutVersion}"
  implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}"

  // 生物识别库，指纹，人脸识别
  implementation "androidx.biometric:biometric:${rootProject.ext.biometricVersion}"

  // 加密工具，需要23版本
  //  implementation "androidx.security:security-crypto:1.0.0-rc01"
  implementation 'com.google.crypto.tink:tink-android:1.5.0'
  implementation 'com.microsoft.identity.client:msal:1.6+'
  implementation "com.zzhoujay.richtext:richtext:${rootProject.ext.richtextVerstion}"
//  implementation 'com.microsoft.identity.client:msal:2.+'
//  implementation 'com.microsoft.graph:microsoft-graph:1.5.+'
}
